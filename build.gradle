plugins {
  id "io.freefair.lombok" version "5.3.0"
  id 'java-library'
  id 'maven-publish'
  id 'com.github.ben-manes.versions' version '0.33.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.15'
}

group = 'net.io_0'
version = "1.1.4"
sourceCompatibility = 11

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation 'ch.qos.logback:logback-classic:1.2.3'
  implementation 'org.ehcache:ehcache:3.9.0'
  implementation 'io.lettuce:lettuce-core:6.0.1.RELEASE'
  implementation 'org.apache.commons:commons-lang3:3.11'
  implementation 'com.github.io-0:maja:1.4.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testImplementation 'org.awaitility:awaitility:4.0.3'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
  useJUnitPlatform()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

// disallow release candidates as upgradable versions from stable
dependencyUpdates.configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { String kw -> version.toUpperCase().contains(kw) }
    return !stableKeyword && !(version ==~ /^[0-9,.v-]+(-r)?$/)
  }

  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
